import net.fabricmc.loom.task.RemapJarTask

plugins {
	id 'fabric-loom' version '0.7-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}


task apiJar(type: Jar, dependsOn: classes) {
	from(sourceSets.main.output)
	include 'blame/**'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}

			pom {
				name = 'Blame-Fabric'
				description = 'Gotta find the broken mods!'
				url = 'https://github.com/TelepathicGrunt/Blame/tree/Fabric-1.16.2'

				scm {
					connection = 'git:https://github.com/TelepathicGrunt/Blame/tree/Fabric-1.16.2.git'
					developerConnection = 'git:https://github.com/TelepathicGrunt/Blame/tree/Fabric-1.16.2.git'
					url = 'https://github.com/TelepathicGrunt/Blame/tree/Fabric-1.16.2'
				}

				licenses {
					license {
						name = 'GNU Lesser General Public License v3.0'
						url = 'https://github.com/TelepathicGrunt/Blame/blob/Fabric-1.16.2/LICENSE'
					}
				}

				developers {
					developer {
						id = 'telepathic_grunt'
						name = 'TelepathicGrunt'
						email = 'asarnesespam@gmail.com'
					}
				}
			}

		}
	}

	repositories {
		maven {
			url = uri("https://nexus.resourcefulbees.com/repository/telepathicgrunt/")
			credentials {
				username "$System.env.MAVEN_USER"
				password "$System.env.MAVEN_PASS"
			}
		}
	}
}
